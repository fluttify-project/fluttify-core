buildscript {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id "com.jfrog.artifactory" version "4.13.0"
}

def GROUP_ID = 'me.yohom'
def ARTIFACT_ID = 'fluttify-core'
def VERSION_NAME = '0.4.1'

group GROUP_ID
version VERSION_NAME

repositories {
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    mavenCentral()
    jcenter()
}

dependencies {
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0-M1'
    implementation 'org.jboss.windup.decompiler:decompiler-fernflower:4.2.1.Final'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.antlr:antlr4-runtime:4.7.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.31"
    // okhttp
    implementation "com.squareup.okhttp3:okhttp:3.14.0"
    // zip https://github.com/zeroturnaround/zt-zip
    implementation 'org.zeroturnaround:zt-zip:1.13'
    // https://github.com/ReactiveX/RxJava
    implementation "io.reactivex.rxjava3:rxjava:3.0.0"
}

publishing {
    publications {
        fluttify(MavenPublication) {
            groupId = GROUP_ID
            artifactId = ARTIFACT_ID
            version = VERSION_NAME

            artifact("/Users/yohom/.m2/repository/me/yohom/fluttify-core/${VERSION_NAME}/fluttify-core-${VERSION_NAME}.jar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    // 如果有compile fileTree()，group会为空，需要去除
                    if (it.group != null) {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'gradle-dev-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }

        defaults {
            publications('fluttify')
            publishArtifacts = true
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}