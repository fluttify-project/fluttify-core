import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'maven'
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}
apply plugin: 'application'
apply plugin: 'c'

group 'me.yohom'
version '0.4.0'

repositories {
    jcenter()
    mavenCentral()
}

jar {
    from('src/main/java') {
        include 'resources/tmpl/**/*.tmpl'
    }
}

model {
    platforms {
        x64 {
            architecture "x64"
        }
    }

    components {
        fluttify_codec(NativeLibrarySpec) {
            binaries.all {
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/darwin"
                    cCompiler.args '-mmacosx-version-min=10.4'
                    linker.args '-mmacosx-version-min=10.4'
                } else if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/linux"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args "-I${Jvm.current().javaHome}/include"
                    cCompiler.args "-I${Jvm.current().javaHome}/include/win32"
                    linker.args "Shlwapi.lib", "Advapi32.lib"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/freebsd"
                }
            }
        }
    }
}

dependencies {
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0-M1'
    implementation 'org.jboss.windup.decompiler:decompiler-fernflower:4.2.1.Final'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.antlr:antlr4-runtime:4.7.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.31"
    // okhttp
    implementation "com.squareup.okhttp3:okhttp:3.14.0"
    // zip https://github.com/zeroturnaround/zt-zip
    implementation 'org.zeroturnaround:zt-zip:1.13'
}